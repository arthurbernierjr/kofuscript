{all, any, concat, concatMap, difference, foldl, foldl1, union} = require './functional-helpers'
{beingDeclared, declarationsFor, usedAsExpression, envEnrichments} = require './helpers'
MS = require './nodes'
exports = module?.exports ? this

makeDispatcher = (defaultValue, handlers, defaultHandler = (->)) ->
  handlers_ = {}
  for [ctors..., handler] in handlers
    handlers_[ctor::className] = handler for ctor in ctors
  (node, args...) ->
    return defaultValue unless node?
    handler =
      if Object::hasOwnProperty.call handlers_, node.className
        handlers_[node.className]
      else defaultHandler
    handler.apply node, args


isTruthy =
  makeDispatcher no, [
    [
      MS.ArrayInitialiser, MS.Class, MS.DeleteOp, MS.ForIn, MS.ForOf
      MS.Function, MS.BoundFunction, MS.HeregExp, MS.ObjectInitialiser, MS.Range
      MS.RegExp, MS.Slice, MS.TypeofOp, MS.While
      -> yes
    ]
    [MS.AssignOp, -> isTruthy @expression]
    [MS.Block, ->
      if @statements.length is 0 then no
      else isTruthy @statements[@statements.length - 1]
    ]
    [MS.Bool, MS.Float, MS.Int, MS.String, -> !!@data]
    [MS.Conditional, ->
      (isTruthy @condition) and (isTruthy @consequent) or
      (isFalsey @condition) and isTruthy @alternate
    ]
    [MS.LogicalAndOp, -> (isTruthy @left) and isTruthy @right]
    [MS.LogicalNotOp, -> isFalsey @expression]
    [MS.LogicalOrOp, -> (isTruthy @left) or isTruthy @right]
    [MS.Program, -> isTruthy @body]
    [MS.SeqOp, -> isTruthy @right]
    [MS.Switch, ->
      (all @cases, isTruthy) and
      if @alternate? then isTruthy @alternate else yes
    ]
    [MS.SwitchCase, -> isTruthy @consequent]
    [MS.UnaryExistsOp, ->
      (isTruthy @expression) or
      # TODO: comprehensive list of all possibly-falsey and always non-null expressions
      @expression.instanceof MS.Int, MS.Float, MS.String, MS.UnaryPlusOp, MS.UnaryNegateOp, MS.LogicalNotOp
    ]
  ], -> no

isFalsey =
  makeDispatcher no, [
    [MS.Null, MS.Undefined, -> yes]
    [MS.AssignOp, -> isFalsey @expression]
    [MS.Block, ->
      if @statements.length is 0 then yes
      else isFalsey @statements[@statements.length - 1]
    ]
    [MS.Bool, MS.Float, MS.Int, MS.String, -> not @data]
    [MS.Conditional, ->
      (isTruthy @condition) and (isFalsey @consequent) or
      (isFalsey @condition) and isFalsey @alternate
    ]
    [MS.LogicalAndOp, -> (isFalsey @left) or isFalsey @right]
    [MS.LogicalNotOp, -> isTruthy @expression]
    [MS.LogicalOrOp, -> (isFalsey @left) and isFalsey @right]
    [MS.Program, -> isFalsey @body]
    [MS.SeqOp, -> isFalsey @right]
    [MS.Switch, ->
      (all @cases, isFalsey) and
      if @alternate? then isFalsey @alternate else yes
    ]
    [MS.SwitchCase, -> isFalsey @block]
    [MS.UnaryExistsOp, -> @expression.instanceof MS.Null, MS.Undefined]
  ], -> no

mayHaveSideEffects =
  makeDispatcher no, [
    [
      MS.Function, MS.BoundFunction, MS.Null, MS.RegExp, MS.This, MS.Undefined
      -> no
    ]
    [
      MS.Break, MS.Continue, MS.Debugger, MS.DeleteOp, MS.NewOp, MS.Return, MS.Super
      MS.PreDecrementOp, MS.PreIncrementOp, MS.PostDecrementOp, MS.PostIncrementOp
      MS.ClassProtoAssignOp, MS.Constructor, MS.Throw, MS.JavaScript, MS.ExtendsOp
      -> yes
    ]
    [MS.Class, (inScope) ->
      (mayHaveSideEffects @parent, inScope) or
      @nameAssignee? and (@name or (beingDeclared @nameAssignee).length > 0)
    ]
    [MS.Conditional, (inScope) ->
      (mayHaveSideEffects @condition, inScope) or
      (not isFalsey @condition) and (mayHaveSideEffects @consequent, inScope) or
      (not isTruthy @condition) and mayHaveSideEffects @alternate, inScope
    ]
    [MS.DoOp, (inScope) ->
      return yes unless @expression.instanceof MS.Functions
      newScope = difference inScope, concatMap @expression.parameters, beingDeclared
      args = for p in @expression.parameters
        if p.instanceof MS.AssignOp then p.expression else p
      return yes if any args, (a) -> mayHaveSideEffects a, newScope
      mayHaveSideEffects @expression.body, newScope
    ]
    [MS.ExistsOp, (inScope) ->
      return yes if mayHaveSideEffects @left, inScope
      return no if @left.instanceof MS.Undefined, MS.Null
      mayHaveSideEffects @right, inScope
    ]
    [MS.FunctionApplication, MS.SoakedFunctionApplication, (inScope) ->
      return yes unless @function.instanceof MS.Function, MS.BoundFunction
      newScope = difference inScope, concatMap @function.parameters, beingDeclared
      return yes if any @arguments, (a) -> mayHaveSideEffects a, newScope
      mayHaveSideEffects @function.body, newScope
    ]
    [MS.LogicalAndOp, (inScope) ->
      return yes if mayHaveSideEffects @left, inScope
      return no if isFalsey @left
      mayHaveSideEffects @right, inScope
    ]
    [MS.LogicalOrOp, (inScope) ->
      return yes if mayHaveSideEffects @left, inScope
      return no if isTruthy @left
      mayHaveSideEffects @right, inScope
    ]
    [MS.While, (inScope) ->
      (mayHaveSideEffects @condition, inScope) or
      (not isFalsey @condition) and mayHaveSideEffects @body, inScope
    ]
    # category: AssignOp
    [MS.AssignOp, MS.ClassProtoAssignOp, MS.CompoundAssignOp, (inScope) ->
      #(mayHaveSideEffects @expression, inScope) or (beingDeclared @assignee).length > 0
      yes
    ]
    # category: Primitive
    [MS.Bool, MS.Float, MS.Identifier, MS.Int, MS.String, -> no]
  ], (inScope) ->
    any @childNodes, (child) =>
      if child in @listMembers
      then any this[child], (m) -> mayHaveSideEffects m, inScope
      else mayHaveSideEffects this[child], inScope



class exports.Optimiser

  @optimise = => (new this).optimise arguments...

  # expose helpers so people have an easy time writing their own rules
  @isTruthy = isTruthy
  @isFalsey = isFalsey
  @mayHaveSideEffects = mayHaveSideEffects

  defaultRules = [

    # If a program has no side effects, then it is the empty program
    [MS.Program, ->
      if !@body? or mayHaveSideEffects @body, [] then this
      else new MS.Program null
    ]

    # Turn blocks into expressions
    [MS.Block, ({inScope}) ->
      switch @statements.length
        when 0 then (new MS.Undefined).g()
        when 1 then @statements[0]
        else
          foldl @statements[0], @statements[1..], (expr, s) ->
            new MS.SeqOp expr, s
    ]

    # Reject unused and inconsequential expressions
    # TODO: comments
    [MS.SeqOp, ({inScope, ancestry}) ->
      canDropLast = not usedAsExpression this, ancestry
      if @left.instanceof MS.Undefined then @right
      else if @left.instanceof MS.Return, MS.Throw then @left
      else if mayHaveSideEffects @left, inScope
        if mayHaveSideEffects @right, inScope then this
        else if not canDropLast then this
        else if @right.instanceof MS.Undefined then @left
        else new MS.SeqOp @left, declarationsFor @right, union inScope, envEnrichments @left, inScope
      else if (@right.instanceof MS.Identifier) and @right.data is 'eval' and
      ((ancestry[0]?.instanceof MS.FunctionApplication) and ancestry[0].function is this or
      (ancestry[0]?.instanceof MS.DoOp) and ancestry[0].expression is this)
        if (@left.instanceof MS.Int) and 0 <= @left.data <= 9 then this
        else if mayHaveSideEffects @left, inScope then this
        else new MS.SeqOp (new MS.Int 0).g(), @right
      else if mayHaveSideEffects @right, inScope
        new MS.SeqOp (declarationsFor @left, inScope), @right
      else if canDropLast
        declarationsFor this, inScope
      else @right
    ]

    # Push assignments through sequences
    [MS.AssignOp, ->
      return this unless @expression.instanceof MS.SeqOp
      new MS.SeqOp @expression.left, new MS.AssignOp @assignee, @expression.right
    ]

    # A falsey condition with side effects -> (the condition; [])
    # A falsey condition without side effects -> []
    # A truthy condition without side effects -> a loop
    [MS.While, ({inScope}) ->
      if isFalsey @condition
        new MS.Block [
          if mayHaveSideEffects @condition, inScope
            new MS.SeqOp @condition, declarationsFor @body
          else
            if @body? then declarationsFor @body, inScope else new MS.Undefined
          new MS.ArrayInitialiser []
        ]
      else if isTruthy @condition
        if mayHaveSideEffects @condition, inScope then this
        else if @body?
          if this instanceof MS.Loop then this else (new MS.Loop @body).g()
        else new MS.ArrayInitialiser []
      else this
    ]

    # Produce the consequent when the condition is truthy
    # Produce the alternative when the condition is falsey
    # Prepend the condition if it has side effects
    [MS.Conditional, ({inScope}) ->
      if isFalsey @condition
        [removedBlock, block] = [@consequent, @alternate]
      else if isTruthy @condition
        [block, removedBlock] = [@consequent, @alternate]
      else
        return this
      decls = declarationsFor removedBlock, inScope
      block = if block? then new MS.SeqOp decls, block else decls
      if mayHaveSideEffects @condition, inScope
        block = new MS.SeqOp @condition, block
      block
    ]

    # for-in over an empty list produces an empty list
    [MS.ForIn, ({inScope}) ->
      return this unless (@target.instanceof MS.ArrayInitialiser) and @target.members.length is 0
      new MS.SeqOp (declarationsFor this, inScope), (new MS.ArrayInitialiser []).g()
    ]

    # for-own-of over empty object produces an empty list
    [MS.ForOf, ({inScope}) ->
      return this unless @isOwn and (@target.instanceof MS.ObjectInitialiser) and @target.members.length is 0
      new MS.SeqOp (declarationsFor this, inScope), (new MS.ArrayInitialiser []).g()
    ]

    # for-in or for-of with falsey filter
    [MS.ForIn, MS.ForOf, ({inScope}) ->
      return this unless isFalsey @filter
      new MS.SeqOp (declarationsFor this, inScope), (new MS.ArrayInitialiser []).g()
    ]

    # for-in or for-of with truthy filter
    [MS.ForIn, ->
      return this unless isTruthy @filter
      new MS.ForIn @valAssignee, @keyAssignee, @target, @step, null, @body
    ]
    [MS.ForOf, ->
      return this unless isTruthy @filter
      new MS.ForOf @isOwn, @keyAssignee, @valAssignee, @target, null, @body
    ]

    # Arrays in statement position might as well be Seqs
    [MS.ArrayInitialiser, ({inScope, ancestry}) ->
      if usedAsExpression this, ancestry then this
      else
        foldl (new MS.Undefined).g(), @members, (expr, m) ->
          new MS.SeqOp expr, m
    ]

    # Produce the right operand when the left operand is null or undefined
    [MS.ExistsOp, -> if @left.instanceof MS.Null, MS.Undefined then @right else this]

    # Produce false when the expression is null or undefined
    [MS.UnaryExistsOp, -> if @expression.instanceof MS.Null, MS.Undefined then (new MS.Bool false).g() else this]

    # LogicalNotOp applied to a literal or !!
    [MS.LogicalNotOp, ({inScope}) ->
      switch
        when @expression.instanceof MS.Int, MS.Float, MS.String, MS.Bool
          (new MS.Bool !@expression.data).g()
        when @expression.instanceof MS.Functions then (new MS.Bool false).g()
        when @expression.instanceof MS.Null, MS.Undefined then (new MS.Bool true).g()
        when @expression.instanceof MS.ArrayInitialiser, MS.ObjectInitialiser
          if mayHaveSideEffects @expression, inScope then this
          else new MS.SeqOp (declarationsFor @expression, inScope), (new MS.Bool false).g()
        when @expression.instanceof MS.LogicalNotOp
          if @expression.expression.instanceof MS.LogicalNotOp then @expression.expression
          else this
        else this
    ]

    # typeof on any literal
    [MS.TypeofOp, ->
      switch
        when @expression.instanceof MS.Int, MS.Float, MS.UnaryNegateOp, MS.UnaryPlusOp
          (new MS.String 'number').g()
        when @expression.instanceof MS.String then (new MS.String 'string').g()
        when @expression.instanceof MS.Functions then (new MS.String 'function').g()
        when @expression.instanceof MS.Undefined then (new MS.String 'undefined').g()
        # TODO: comprehensive
        else this
    ]

    # simplify trailing `return`/`undefined` in function bodies
    [MS.SeqOp, ({ancestry}) ->
      return this unless (ancestry[0]?.instanceof MS.Functions) and ancestry[0].body is this
      if (@right.instanceof MS.Return) and @right.expression?
        new MS.SeqOp @left, @right.expression
      else if @right.instanceof MS.Undefined
        new MS.SeqOp @left, new MS.Return
      else this
    ]

    # get rid of function bodies that are simply `return` or `undefined`
    [MS.Function, MS.BoundFunction, ->
      return this unless @block? and (
        (@block.instanceof MS.Undefined) or
        (@block.instanceof MS.Return) and not @block.expression?
      )
      new @constructor @parameters, null
    ]

    # `return undefined` -> `return`, everywhere
    [MS.Return, -> if @expression?.instanceof MS.Undefined then new MS.Return else this]

    [MS.Slice, ->
      if (@left?.instanceof MS.Int, MS.String) and +@left.data is 0
        new MS.Slice @expression, @isInclusive, null, @right
      else if @isInclusive and (@right?.instanceof MS.UnaryNegateOp) and (@right.expression.instanceof MS.Int) and @right.expression.data is 1
        new MS.Slice @expression, yes, @left, null
      else this
    ]
  ]

  constructor: ->
    @rules = {}
    for [ctors..., handler] in defaultRules
      for ctor in ctors
        @addRule ctor::className, handler

  addRule: (ctor, handler) ->
    (@rules[ctor] ?= []).push handler
    this

  optimise: do ->

    walk = (fn, inScope = [], ancestry = []) ->
      ancestry.unshift this
      for childName in @childNodes when this[childName]?
        if childName in @listMembers
          for member, n in this[childName]
            while this[childName][n] isnt walk.call (this[childName][n] = fn.call this[childName][n], {inScope, ancestry}), fn, inScope, ancestry then
            inScope = union inScope, envEnrichments this[childName][n], inScope
        else
          while this[childName] isnt walk.call (this[childName] = fn.call this[childName], {inScope, ancestry}), fn, inScope, ancestry then
          inScope = union inScope, envEnrichments this[childName], inScope
      do ancestry.shift
      replacementNode = fn.call this, {inScope, ancestry}
      if this isnt replacementNode
        while replacementNode isnt walk.call (replacementNode = fn.call replacementNode, {inScope, ancestry}), fn, inScope, ancestry then
        replacementNode[p] = this[p] for p in ['raw', 'line', 'column', 'offset']
      replacementNode

    (ast) ->
      rules = @rules
      walk.call ast, ({ancestry}) ->
        if not this? or this is global
          throw new Error 'Optimiser rules must produce a node. `null` is not a node.'
        return this if this in ancestry
        # not a fold for efficiency's sake
        memo = this
        for rule in rules[memo.className] ? []
          memo = rule.apply memo, arguments
          break if memo isnt this
        memo
