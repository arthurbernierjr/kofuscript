child_process = require 'child_process'
fs = require 'fs'
path = require 'path'

MambaScript = require './module'
{runModule} = require './run'

module.exports = not require.extensions['.coffee']?

{argv} = require('kofu-optimist')
  .boolean('self')

compile = (module, filename, opts) ->
  input = fs.readFileSync filename, 'utf8'
  csAst = MambaScript.parse input, opts
  jsAst = MambaScript.compile csAst
  js = MambaScript.js jsAst
  runModule module, js, jsAst, filename

require.extensions['.coffee'] = (module, filename) ->
  compile module, filename, raw: yes

require.extensions['.litcoffee'] = (module, filename) ->
  compile module, filename, raw: yes, literate: yes

require.extensions['.mamba'] = (module, filename) ->
  compile module, filename, raw: yes, typeCheck: true, literate: yes

# patch child_process.fork to default to the mamba binary as the execPath for coffee/litcoffee/mamba files
{fork} = child_process
unless fork.mambaPatched
  mambaBinary = path.resolve 'bin', 'mamba'
  child_process.fork = (file, args = [], options = {}) ->
    if (path.extname file) in ['.coffee', '.litcoffee', '.mamba']
      if not Array.isArray args
        args = []
        options = args or {}
      options.execPath or= mambaBinary
    fork file, args, options
  child_process.fork.mambaPatched = yes

delete require.cache[__filename]
